plugins {
	id "maven-publish"
	id "fabric-loom" version "1.3-SNAPSHOT"
	id "io.github.juuxel.loom-vineflower" version "1.11.0"
	id 'me.modmuss50.mod-publish-plugin' version '0.3.4'
}


repositories {
	mavenCentral()
	maven {
		url "https://masa.dy.fi/maven"
	}
	maven {
		url "https://pkgs.dev.azure.com/djtheredstoner/DevAuth/_packaging/public/maven/v1"
	}
	maven {
		url 'https://www.cursemaven.com'
	}
}

version = minecraft_version + "-" + mod_version
group = project.maven_group

dependencies {
	minecraft "com.mojang:minecraft:${minecraft_version}"
	mappings "net.fabricmc:yarn:${yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
}

loom {
	accessWidenerPath = file("src/main/resources/chunkdebug.accesswidener")
}

processResources {
	inputs.property "version", mod_version

	filesMatching("fabric.mod.json") {
		def map = [
			"version"             : mod_version,
			"minecraft_dependency": minecraft_dependency
		]
		expand map
	}
}

tasks.withType(JavaCompile).configureEach {
	options.encoding = "UTF-8"
	it.options.release.set(16)
}

java {
	withSourcesJar()
}

jar {
	from "LICENSE"
}

publishMods {
	file = remapJar.archiveFile
	changelog = """
	- Updated to 1.20.4
	"""
	type = STABLE
	modLoaders.add("fabric")

	displayName = "ChunkDebug ${minecraft_version} v${mod_version}"

	modrinth {
		accessToken = providers.environmentVariable("MODRINTH_API_KEY")
		projectId = "zQxjhDPq"
		minecraftVersions.add(minecraft_version)

		requires {
			id = "P7dR8mSH"
		}
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			artifact(jar) {
				builtBy remapJar
			}
		}
	}
}
